/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	cr�ation : 13 f�vrier 2002
 *
 *     nature : projet etudiant
 *Groupe 1.1
 * CHAPON Théo
 * EL OMARI ALAOUI Hassan
 * MARCHAIS Julien
 * MESDOURI Marouan
 * TUEKAM Marlène
 * Février - Mars 2014
 */


options{
	JDK_VERSION="1.6"; /* 1.5 */
}

PARSER_BEGIN(Yaka)



public class Yaka {
	public static void main(String args[]) {
		Yaka analyseur;
		java.io.InputStream input;
		java.io.OutputStream output;
		java.io.File temp;
		java.io.InputStream tmp = null;
		String fichierOut;
		if (args.length==2) {
			System.out.print(args[0] + ": ");
			try {
				input = new java.io.FileInputStream(args[0] + ".yaka"); /* avant : args.lentgh -1 */
				fichierOut = args[1] + ".asm";
			} catch (java.io.FileNotFoundException e) {
				System.out.println("Fichier introuvable.");
				return;
			}
		} else if (args.length==1) {
			fichierOut = args[0] + ".asm";
			System.out.println("Lecture sur l'entree standard...");
			input = System.in;
		} else {
			System.out.println("Usage: java Yaka [fichier entrée] [fichier sortie] ou java Yaka [fichier sortie]");
			return;
		}
		try {
			analyseur = new Yaka(input);
			analyseur.analyse();
			System.out.println("analyse syntaxique reussie!");
			temp = new java.io.File("yvm.tmp");
			if (Erreur.err) {
				System.out.println("Compilation impossible");
				System.out.println("La g\u00e9n\u00e9ration du fichier a \u00e9chou\u00e9");
			}
			else {
				output = Ecriture.ouvrir(fichierOut);
				try {
					tmp = new java.io.FileInputStream(temp);
				} catch (java.io.FileNotFoundException e) {
					e.printStackTrace();
				}
				output = Ecriture.ouvrir(fichierOut);
				Ecriture.ecrireFichier(output,tmp);
				Ecriture.fermer(output);
				Lecture.fermer(tmp);
				System.out.println("Le fichier `" + fichierOut + "` a \u00e9t\u00e9 g\u00e9n\u00e9r\u00e9 avec succ\u00e8s");
			}
			temp.delete(); /* suppression du ficher temporaire */
		} catch (ParseException e) {
			String msg = e.getMessage();
			msg = msg.substring(0,msg.indexOf("\u005cn"));
			System.out.println("Erreur de syntaxe : "+msg);
		}
	}
}
PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{
public static String identLu,chaineLue;
public static int entierLu;
public static Declaration declaration= new Declaration();
public static Expression expression = new Expression();
public static TabIdent tabident;
public static YVM yvm = new YVMasm();
}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots r�serv�s*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME : "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}
/* Operateurs */
TOKEN :
{
	<ADD : "+">
	| <SUBNEG : "-">
	| <OR : "OR">
	| <MUL : "*">
	| <DIV : "/">
	| <AND : "AND">
	| <NOT : "NOT">
	| <EGAL : "=">
	| <DIFF : "<>">
	| <INF : "<">
	| <INFEGAL : "<=">
	| <SUP : ">">
	| <SUPEGAL : ">=">
}

/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre : ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{
	{
		YakaTokenManager.tabident = new TabIdent();
		YakaTokenManager.tabident = new TabIdent();
		YakaTokenManager.tabident = new TabIdent();
		YakaTokenManager.yvm.entete();
	}
   <PROGRAMME>
   <ident>	
   bloc()
   <FPROGRAMME>
   {
		YakaTokenManager.yvm.queue();
	}
}

void bloc() : {}{
 (declConst())*
 (declVar())*
	{YakaTokenManager.yvm.ouvrePrinc(YakaTokenManager.tabident.nbVar() * 2);}
   suiteInstr() 
}

void declConst() : {}
{
  <CONST> defConst() ( "," defConst())* ";"
  
}

void defConst() : {}
{
	
  <ident> 
  {
  	YakaTokenManager.declaration.ajoutNomConstante(YakaTokenManager.identLu);
  }
   "=" valConst()
 
}

void valConst() : {}{
  <entier>
   {
   	YakaTokenManager.declaration.ajoutConstanteParEntier(YakaTokenManager.entierLu);
  }
  |"-" <entier>
  {
   	YakaTokenManager.declaration.ajoutConstanteParEntier(YakaTokenManager.entierLu*-1);
  }
  | <ident>
  {
  	YakaTokenManager.declaration.ajoutConstanteParConstante(YakaTokenManager.identLu);
  }
 | <VRAI> 
  {
  	YakaTokenManager.declaration.ajoutConstanteParBooleen(-1);
  }		
 | <FAUX> 
  {
  	YakaTokenManager.declaration.ajoutConstanteParBooleen(0);
  }		
}


void declVar() : {}
{
  <VAR>
  type()
  <ident>
  {
	YakaTokenManager.declaration.ajoutNomVariable(YakaTokenManager.identLu);
  }
  ("," 
      <ident>
      {
	YakaTokenManager.declaration.ajoutNomVariableSecondaire(YakaTokenManager.identLu);
      }
  )* ";"
	
}

void type() :{}
{
   <ENTIER>
  	{
  		YakaTokenManager.declaration.ajoutVariableParTYPE("ENTIER");
  	}		
 | <BOOLEEN>	
  	{
  		YakaTokenManager.declaration.ajoutVariableParTYPE("BOOLEEN");
  	}
}
/*
 * Syntaxe des instructions.
 */
void suiteInstr() : {}
{
	instruction()
	(
		";" (
				instruction()
			)?
	)*
}


void instruction() : {}
{
	affectation() | lecture() | ecriture()
}

void affectation() : {}
{
	<ident>
	{
		String id = YakaTokenManager.identLu;
	}
	
	"="
	
	expression()
	{
		YakaTokenManager.expression.store(id);
	/* Vider les piles pour évaluer une autre expression */
	YakaTokenManager.expression.clearType();
	YakaTokenManager.expression.clearOp();
	}
}

void lecture() : {}
{
	<LIRE> "(" <ident> ")"
	{
		YakaTokenManager.expression.lire(YakaTokenManager.identLu);
	}
}

void ecriture() : {}
{
	<ECRIRE> "("( 
	expression()
	{
		YakaTokenManager.expression.ecrire();
	}
	| 
	<chaine> 
	{
		YakaTokenManager.yvm.ecrireChaine(YakaTokenManager.chaineLue);
	}
	)")" | 
	<ALALIGNE>
	{
		YakaTokenManager.yvm.aLaLigne();
	}
}


/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
(
  opRel()
  simpleExpr() 
{
	YakaTokenManager.expression.testRel();
	YakaTokenManager.expression.executerOpRel();
} 
  )?
{
}
}



void simpleExpr() : {}
{
   terme() 
   (
   
   opAdd()
   terme()
   {
	   
	   YakaTokenManager.expression.testAdd();
	   YakaTokenManager.expression.executerOpAdd();
	}
   )*
} 
void terme() : {}
{
  facteur() 
  (
  opMul()
  facteur()
   {
	   
	   YakaTokenManager.expression.testMul();
	   YakaTokenManager.expression.executerOpMul();
	}
  )*
}
void facteur() : {}
{   primaire() 
  |  
  opNeg()
	primaire()
  {
	  
	   YakaTokenManager.expression.testNeg();
	  YakaTokenManager.expression.executerOpNeg();
	}
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () : {}
{ <entier> 	
	{
		YakaTokenManager.expression.empileType("entier");
		YakaTokenManager.yvm.iconst(YakaTokenManager.entierLu);
	}
 | <ident>
	 {
		YakaTokenManager.expression.empileTypeAvecIdent(YakaTokenManager.identLu);
		YakaTokenManager.expression.loadIdent(YakaTokenManager.identLu);
	 }
 | <VRAI> 
	{
		YakaTokenManager.expression.empileType("booleen");
		YakaTokenManager.yvm.iconst(-1);
	}
 | <FAUX> 
	{
		YakaTokenManager.expression.empileType("booleen");
		YakaTokenManager.yvm.iconst(0);
	}
 
}

void opRel() : {}
{
   "=" 	
  {
	  YakaTokenManager.expression.empileOpera("=");
	  YakaTokenManager.expression.setLastOpRel("=");
	}
 | "<>" 
  {
	  YakaTokenManager.expression.empileOpera("<>");
	  YakaTokenManager.expression.setLastOpRel("<>");
  }
 | "<" 	
  {
	  YakaTokenManager.expression.empileOpera("<");
	  YakaTokenManager.expression.setLastOpRel("<");
  }
 | "<=" 
  {
	  YakaTokenManager.expression.empileOpera("<=");
	  YakaTokenManager.expression.setLastOpRel("<=");
  }
 | ">" 	
  {
	  YakaTokenManager.expression.empileOpera(">");
	  YakaTokenManager.expression.setLastOpRel(">");
  }
 | ">=" 
  {
	  YakaTokenManager.expression.empileOpera(">=");
	  YakaTokenManager.expression.setLastOpRel(">=");
  }
} 

void opAdd() : {}
{
    "+"
  {
	  YakaTokenManager.expression.empileOpera("+");
	  YakaTokenManager.expression.setLastOpAdd("+");
	}
  |  "-" 
  {
	  YakaTokenManager.expression.empileOpera("-");
	  YakaTokenManager.expression.setLastOpAdd("-");
	}
  | <OU> 
  {
	  YakaTokenManager.expression.empileOpera("ou");
	  YakaTokenManager.expression.setLastOpAdd("OR");
	}
}

void opMul() : {}
{
  "*"	
  {
	YakaTokenManager.expression.empileOpera("*");
	  YakaTokenManager.expression.setLastOpMul("*");
  }
  | "/"	
  {
	YakaTokenManager.expression.empileOpera("/");
	  YakaTokenManager.expression.setLastOpMul("/");
  }
  | <ET>	
  {
	  YakaTokenManager.expression.empileOpera("et");
	  YakaTokenManager.expression.setLastOpMul("AND");
	}
} 

void opNeg() : {}
{ "-" 
  {
	  YakaTokenManager.expression.empileOpera("-");
	  YakaTokenManager.expression.setLastOpNeg("-");
  }
 | <NON>
  {
	  YakaTokenManager.expression.empileOpera("non");
	  YakaTokenManager.expression.setLastOpNeg("NOT");
  }
}
/* on ne peut pas faire opneg pour une expression */
